
// ROOM 2 


// ESTADOS INICIALES
bool bAbrigoPaxtonCogido = false;
bool bAbrigoBarnesCogido = false;
bool bTijerasCogidas = false;
bool bViniloCogido = false;
bool bViniloColocado = false; 
bool bDiscoEscuchado = false;

//PANEL DIGITAL
import bool panelActivo;
import bool bPanelResuelto;
import int codigoDigito1, codigoDigito2, codigoDigito3, codigoDigito4;
import function ActualizarPanelGUI();

// Pistas cuadros
bool bCuadro1Examinado = false;
bool bCuadro2Examinado = false;
bool bCuadro3Examinado = false;
bool bCuadro4Examinado = false;

// Pistas libros
bool bLibro1Examinado = false;
bool bLibro2Examinado = false;
bool bLibro3Examinado = false;
bool bLibro4Examinado = false;

// PRIMERA VEZ EN ROOM
bool primeraVezRoom2 = true;

// ==========================
// VERIFICAR PISTAS
// ==========================
function VerificarPistas()
{
    if (bCuadro1Examinado && bCuadro2Examinado &&
        bCuadro3Examinado && bCuadro4Examinado &&
        bLibro1Examinado && bLibro2Examinado && bLibro3Examinado && bLibro4Examinado)
    {
        cPaxton.Say("Creo que ya lo tengo...");
        cPaxton.Say("Código: 4 - 7 - 4 - 2");
    }
}

// ==========================
// HOTSPOT PANEL DIGITAL
// ==========================

function hPanel_LookAt()
{
    if (!panelActivo)
        cBarnes.Say("Parece que está conectado a la puerta, probemos a usarlo.");
    else
        cBarnes.Say("Definitivamente tiene que estar relacionado con la puerta.");
}

function hPanel_Interact()
{
    panelActivo = true;
    gPanelCodigo.Visible=true;
    ActualizarPanelGUI();
    cPaxton.Say("Necesito ingresar el código de cuatro dígitos.");
}

function hPanel_MouseMove()
{
    cPaxton.Say("Panel digital");
}
// ==========================
// ABRIGOS
// ==========================

function oAbrigoPaxton_LookAt()
{
    if (!bAbrigoPaxtonCogido)
        cBarnes.Say("¡Mira!, es tu abrigo Paxton");
    else
        cBarnes.Say("Aquí estaba tu abrigo, pero ya lo has cogido.");
}

function oAbrigoBarnes_LookAt()
{
    if (!bAbrigoBarnesCogido)
        cBarnes.Say("¡Mira!, es mi abrigo");
    else
        cBarnes.Say("Aquí estaba mi abrigo, pero ya lo he cogido.");
}

function oAbrigoPaxton_Interact()
{
    if (!bAbrigoPaxtonCogido)
    {
        bAbrigoPaxtonCogido = true;
        
        // Ocultar solo el abrigo de Paxton
        oAbrigoPaxton.Visible = false;
        
        // Añadir al inventario
        player.AddInventory(iAbrigoPaxton);
        
        cPaxton.Say("He guardado mi abrigo.");
        
        // Verificar si ambos abrigos están cogidos
        if (bAbrigoPaxtonCogido && bAbrigoBarnesCogido)
        {
            cBarnes.Say("Perfecto, ahora estamos listos para salir.");
        }
        else if (!bAbrigoBarnesCogido)
        {
            cBarnes.Say("Vale, ahora voy a coger el mío.");
        }
    }
    else
    {
        cPaxton.Say("Ya tengo mi abrigo.");
    }
}

function oAbrigoBarnes_Interact()
{
    if (!bAbrigoBarnesCogido)
    {
        bAbrigoBarnesCogido = true;
        
        // Ocultar solo el abrigo de Barnes
        oAbrigoBarnes.Visible = false;
        
        // Añadir al inventario
        player.AddInventory(iAbrigoBarnes);
        
        cBarnes.Say("He guardado mi abrigo.");
        
        // Verificar si ambos abrigos están cogidos
        if (bAbrigoPaxtonCogido && bAbrigoBarnesCogido)
        {
            cPaxton.Say("Perfecto, ahora estamos listos para salir.");
        }
        else if (!bAbrigoPaxtonCogido)
        {
            cPaxton.Say("Vale, ahora voy a coger el mío.");
        }
    }
    else
    {
        cBarnes.Say("Ya tengo mi abrigo.");
    }
}


function oAbrigoPaxton_OnClick()
{
    if (!bAbrigoPaxtonCogido)
        cPaxton.Say("Abrigo de Paxton");
    else
        cPaxton.Say("Aquí estaba el abrigo de Paxton");
}

function oAbrigoBarnes_OnClick()
{
    if (!bAbrigoBarnesCogido)
        cPaxton.Say("Abrigo de Barnes");
    else
        cPaxton.Say("Aquí estaba el abrigo de Barnes");
}

// ==========================
// CUADROS (LookAt + MouseMove)
// ==========================
function hCuadro1_LookAt()
{
    cPaxton.Say("La inscripción dice “Horus caminando sobre el agua, 4000 a.C.”. Parece que el número “4” está resaltado con marcador rojo.");
    codigoDigito1 = 4; bCuadro1Examinado = true; VerificarPistas();
}
function hCuadro1_MouseMove() { cPaxton.Say("Cuadro egipcio"); }

function hCuadro4_LookAt()
{
    cPaxton.Say("La inscripción dice “Mita hacedor de milagros, 703 a.C.”. El número “7” está resaltado.");
    codigoDigito2 = 7; bCuadro4Examinado = true; VerificarPistas();
}
function hCuadro4_MouseMove() { cPaxton.Say("Cuadro romano"); }

function hCuadro2_LookAt()
{
    cPaxton.Say("La inscripción dice “Krishna, el hombre que salió del infierno después de morir, 1200 a.C.”. El número “2” está resaltado.");
    codigoDigito4 = 2; bCuadro2Examinado = true; VerificarPistas();
}
function hCuadro2_MouseMove() { cPaxton.Say("Cuadro hindú"); }

function hCuadro3_LookAt()
{
    cPaxton.Say("La inscripción dice “Los dioses griegos cenando en la mesa del Olimpo, 4000 a.C.”. El número “4” está resaltado.");
    codigoDigito3 = 4; bCuadro3Examinado = true; VerificarPistas();
}
function hCuadro3_MouseMove() { cPaxton.Say("Cuadro griego"); }

// ==========================
// LIBROS (LookAt + MouseMove)
// ==========================
function hLibro1_LookAt()
{
    cPaxton.Say("Libro antiguo, cuenta la historia de un mesías que caminaba sobre el agua, el libro fue escrito en el año 100 d.C.");
    bLibro1Examinado = true; VerificarPistas();
}
function hLibro1_MouseMove() { cPaxton.Say("Libro antiguo"); }

function hLibro2_LookAt()
{
    cPaxton.Say("Libro desgastado, cuenta sobre un mesías que cenaba con sus discípulos, el libro fue escrito en el año 300 d.C.");
    bLibro2Examinado = true; VerificarPistas();
}
function hLibro2_MouseMove() { cPaxton.Say("Libro desgastado"); }

function hLibro3_LookAt()
{
    cPaxton.Say("Libro sagrado descolorido, cuenta sobre un mesías que realizaba milagros por donde pasaba, el libro fue escrito en el año 200 d.C.");
    bLibro3Examinado = true; VerificarPistas();
}
function hLibro3_MouseMove() { cPaxton.Say("Libro sagrado"); }

function hLibro4_LookAt()
{
    cPaxton.Say("Libro sagrado contemporáneo, parece ser recién publicado. Cuenta sobre un mesías que salió del infierno tras morir.");
    bLibro4Examinado = true; VerificarPistas();
}
function hLibro4_MouseMove() { cPaxton.Say("Libro contemporáneo"); }

// ==========================
// DISCO DE VINILO (LookAt + Interact + MouseMove)
// ==========================
function oVinilo_LookAt()
{
    cPaxton.Say("Tiene una nota que dice “Reprodúceme”.");
}
function oVinilo_Interact()
{
    if (!bViniloCogido)
    {
        cPaxton.Say("Voy a guardar el disco en mi inventario.");
        bViniloCogido = true;
        // player.AddInventory(iVinilo);
        oVinilo.Visible = false;
    }
    else cPaxton.Say("Ya tengo el disco.");
}
function oVinilo_OnClick() { cPaxton.Say("Disco de vinilo"); }

// ==========================
// TOCADISCOS
// ==========================

function oTocadiscos_LookAt()
{
    if (!bViniloColocado)
        cPaxton.Say("Mira, un tocadiscos, parece en buen estado.");
    else
        cPaxton.Say("El disco ya está reproduciéndose.");
}
// ==========================
// MENSAJE DEL DISCO
// ==========================
function MostrarMensajeDisco()
{
    cSrReed.Say("========================================");
    cSrReed.Say("Hola, se me escucha?, bueno.");
    cSrReed.Say("Hola, Barnes y Paxton. Si escuchan esto, ya son parte de mi pequeño experimento.");
    cSrReed.Say("Tienen que encontrar la contraseña de cuatro dígitos para abrir la puerta del sótano.");
    cSrReed.Say("Busquen entre las creencias del mundo… Todas son la misma mentira contada con distintos nombres.");
    cSrReed.Say("========================================");
}


function oTocadiscos_Interact()
{
    if (!bViniloCogido)
    {
        cPaxton.Say("Necesito primero el disco de vinilo para reproducirlo.");
        return;
    }

    if (!bViniloColocado)
    {
        cPaxton.Say("Voy a poner el disco...");
        bViniloColocado = true;

        // Ocultar tocadiscos sin disco
        oTocadiscosSin.Visible = false;
        
        // Mostrar tocadiscos con disco
        oTocadiscosCon.Visible = true;

        // Quitar el vinilo del inventario si lo tienes
        // player.LoseInventory(iVinilo);

        // Reproducir mensaje del disco
        MostrarMensajeDisco();
    }
    else
    {
        cPaxton.Say("El disco ya está reproduciéndose.");
    }
}

function oTocadiscos_OnClick()
{
    cPaxton.Say("Tocadiscos antiguo");
}


// ==========================
// PUERTA DEL SÓTANO (Interact + MouseMove)
// ==========================
function hPuertaSotano_Interact()
{
    if (bPanelResuelto)
    {
        cPaxton.Say("La puerta está abierta. Bajemos.");
        player.ChangeRoom(3);
    }
    else cPaxton.Say("Está bloqueada. Necesito abrir el panel digital primero.");
}
function hPuertaSotano_MouseMove() { cPaxton.Say("Puerta vieja"); }
// ==========================
// TIJERAS
// ==========================

function oTijeras_LookAt()
{
    if (!bTijerasCogidas)
    {
        cBarnes.Say("Deberíamos de cogerlas, nunca está de más ser precavidos.");
    }
    else
    {
        cPaxton.Say("Ya las he cogido.");
    }
}

function oTijeras_Interact()
{
    if (!bTijerasCogidas)
    {
        bTijerasCogidas = true;

        // Añadir al inventario
        // player.AddInventory(iTijeras);

        oTijeras.Visible = false;  
        cBarnes.Say("Las guardamos por si acaso, a lo mejor son útiles luego");
    }
    else
    {
        cPaxton.Say("Ya tengo las tijeras.");
    }
}

function oTijeras_OnClick()
{
    cPaxton.Say("Tijeras");
}


// ==========================
// ROOM AFTER FADE IN
// ==========================
function room_AfterFadeIn()
{
    if (primeraVezRoom2)
    {
        Wait(20);
        cPaxton.Say("Esta debe de ser la capilla del Sr. Reed...");
        cBarnes.Say("Mira todos esos libros.");
        cPaxton.Say("Y los cuadros... parecen de distintas religiones.");
        primeraVezRoom2 = false;
    }
}
